apply plugin: 'groovy'
apply plugin: 'maven-publish'

ext {
  artifactRepoBase = 'http://oss.jfrog.org/artifactory'
  artifactLabel = 'gradle-jvm-src-plugin'
  androidToolsVersion = project.hasProperty('androidToolsVersion') ? project.property('androidToolsVersion') : '0.11.+'
}

repositories {
  mavenCentral()
}


dependencies {
  //Gradle uses groovy 1.8.6 for the buildSrc project so an earlier spock version is required
  testCompile "org.spockframework:spock-core:0.7-groovy-1.8"
  testCompile "com.android.tools.build:gradle:$androidToolsVersion"
  compile localGroovy()
  compile gradleApi()
}

task sourceJar(type: Jar) {
  from sourceSets.main.allJava
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId artifactLabel
      groupId 'org.ajar.gradle'
      artifactId "$artifactLabel"

      from components.java

      artifact sourceJar {
        classifier "sources"
      }
    }
  }

  repositories {
    maven {
      name 'jcenter'
      url "${artifactRepoBase}/oss-${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}-local"

      credentials {
        username = 'adrianbkelly'
        password = project.hasProperty('bintray_key') ? project.getProperty('bintray_key') : ''
      }
    }

    if (!project.version.endsWith('-SNAPSHOT')) {
      maven {
        name 'bintray'
        url "https://api.bintray.com/maven/adrianbkelly/maven/${artifactLabel}"
        credentials {
          username = 'adrianbkelly'
          password = project.hasProperty('bintray_key') ? project.getProperty('bintray_key') : ''
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.12'
}